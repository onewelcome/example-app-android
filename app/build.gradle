/*
 * Copyright (c) 2016-2022 Onegini B.V.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
    // REQUIRED: Google's new Maven repo is required for the latest
    // support library that is compatible with Android 8.0
    google()
    if (project.hasProperty('artifactory_user') && project.hasProperty('artifactory_password')) {
      maven {
        /*
        Before the release please change the url below to: https://repo.onewelcome.com/artifactory/onegini-sdk
        Please change it back to https://repo.onewelcome.com/artifactory/public after the release
        */
        url "https://repo.onewelcome.com/artifactory/public"
        credentials {
          username artifactory_user
          password artifactory_password
        }
      }
    } else {
      throw new InvalidUserDataException("You must configure the 'artifactory_user' and 'artifactory_password' properties in your project before you can " +
          "build it.")
    }
  }
}

android {
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    applicationId "com.onegini.mobile.exampleapp"
    minSdkVersion 24
    targetSdk 36
    compileSdk 36
    versionCode 25
    versionName "7.11.0"
    multiDexEnabled true
  }
  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-example-app.pro'
    }
  }
  packagingOptions {
    exclude 'META-INF/rxjava.properties'
  }
  lintOptions {
    abortOnError false
  }
  namespace 'com.onegini.mobile.exampleapp'
}

dependencies {
  // Onegini SDK
  debugApi('com.onegini.mobile.sdk.android:onegini-sdk-developer:13.2.0-SNAPSHOT@aar') {
    transitive = true
  }
  api('com.onegini.mobile.sdk.android:onegini-sdk:13.2.0-SNAPSHOT@aar') {
    transitive = true
  }
  // Rest Client
  implementation 'com.squareup.retrofit2:adapter-rxjava3:2.11.0'

  // Google Support libraries for UI
  implementation 'com.google.android.material:material:1.12.0'
  implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

  // QR code reader
  implementation 'com.google.android.gms:play-services-vision:20.1.3'
  // Google Play Services availability check
  implementation 'com.google.android.gms:play-services-base:18.5.0'
  //Firebase Cloud Messaging (FCM)
  implementation 'com.google.firebase:firebase-messaging:25.0.0'
  // Support for multidex
  implementation 'androidx.multidex:multidex:2.0.1'
  // DI for views
  implementation 'com.jakewharton:butterknife:10.2.0'
  annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'
  // RxJava
  implementation 'io.reactivex.rxjava3:rxjava:3.1.9'
  implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'

  //Biometric
  implementation "androidx.biometric:biometric:1.1.0"
}

//according to google documentation, this plugin has to be applied at the end of the file
apply plugin: 'com.google.gms.google-services'
